namespace AB

types
    ABProgram;
    Integer, String     <: Type;
    Decl;
    ClearStmt, HandlerStmt, IfStmt, MapStmt, OverlayStmt, ReturnStmt
                        <: Stmt;
    Var, Num            <: Expr;

tokens
  ' ', '\r', '\n' :: skip
  ';', '(', ')' :: mark
  'dcl', 'enddcl', 'integer', 'char',
  'clear', 'handler', 'if', 'endif', 'map', 'to', 'overlay', 'return' :: word
  number :: Num
  string :: Var

handlers
    EOF                 -> push ABProgram(data,code)
                           where code := pop# Stmt,
                                 data := pop# Decl
    'dcl'               -> lift DCL
    'enddcl'            -> drop DCL
    ';' given DCL       -> push Decl(v,t)
                           where t := pop Type,
                                 v := pop Var
    'integer' given DCL -> push Integer
    'char'    given DCL -> push String(n)
                           where n := await (Num given BRACKET) with CHAR
    '(' given CHAR      -> lift BRACKET
    ')' given CHAR      -> drop BRACKET

    'clear'             -> push ClearStmt(view)
                           where view := await Var
    'handler'           -> push HandlerStmt(obj,proc)
                           where obj  := await Var,
                                 proc := await (Var given BRACKET) with HANDLER
    '(' given HANDLER   -> lift BRACKET
    ')' given HANDLER   -> drop BRACKET
    'if'                -> push IfStmt(cond,branch)
                           where cond   := await Expr,
                                 branch := await* Stmt
    'endif'             -> trim Stmt*
    'map'               -> push MapStmt(source,target)
                           where source := await Expr with MAP,
                                 target := await Var with MAP
    'overlay'           -> push OverlayStmt(source,target)
                           where source := await Expr with MAP,
                                 target := await Var with MAP
    'return'            -> push ReturnStmt

%    V given MAP         -> assert exists Decl(v,t) such that v == this
